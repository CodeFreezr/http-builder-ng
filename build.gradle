plugins {
    id 'com.jfrog.bintray' version '1.7'
}


apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'groovy'

sourceCompatibility = 8
targetCompatibility = 8

group = 'org.codehaus.groovy.modules'
version = '0.9.6'

repositories {
    jcenter()
}

configurations {
    runtime {
        exclude(module: 'signpost-commonshttp4')
        exclude(module: 'signpost-core')
        exclude(module: 'httpclient')
        exclude(module: 'groovy-all')
    }

    testRuntime.extendsFrom = [compile, testCompile]
}

dependencies {
    //required
    compile('net.sourceforge.nekohtml:nekohtml:1.9.22');
    compile('xml-resolver:xml-resolver:1.2')
    compile('org.slf4j:slf4j-api:1.7.21')
    
    //optionals
    compile('com.opencsv:opencsv:3.8');
    compile('org.apache.httpcomponents:httpclient:4.5.2');
    compile('oauth.signpost:signpost-commonshttp4:1.2.1.1')
    compile('oauth.signpost:signpost-core:1.2.1.1')
    compile('org.codehaus.groovy:groovy-all:2.4.6')

    testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
}

javadoc {
    options.links("http://docs.oracle.com/javase/8/docs/api/", "http://docs.groovy-lang.org/latest/html/gapi/");
}

task sourcesJar(type: Jar) {
    description = 'Creates sources JAR.'
    classifier = 'sources'
    from project.sourceSets.main.allSource
}

artifacts {
    archives jar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
            artifact sourcesJar
            version project.version
        }
    }
}

bintray {
    def bintrayUser = project.hasProperty('user') ? project.property('user') : null;
    def bintrayKey = project.hasProperty('key') ? project.property('key') : null;

    if(bintrayUser && bintrayKey) {
        user = bintrayUser
        key = bintrayKey
        
        publications = ['mavenJava']
        
        pkg {
            repo = 'maven'
            name = 'http-builder-ng'
            desc = 'Groovy client for making http requests'
            licenses = ['Apache-2.0']
            labels = ['groovy']
            vcsUrl = 'https://github.com/dwclark/httpbuilder'
        }
    }
}
